Name     ide;
PartNo   NA;
Date     16/04/2024;
Revision 01;
Designer Malcolm Harrow / Mark Murray;
Company  NA;
Assembly NA;
Location NA;
Device   f1504ispplcc84;
 
/* *************** INPUT PINS *********************/
PIN = [A19..1];
PIN = !IOSEL;
PIN = CLOCK;
PIN = [FC0..2];
PIN = !LDS;
PIN = !UDS;
PIN = R_nW;

/* *************** OUTPUT PINS *********************/
PIN = DTACK;
PIN = !LSEL;
PIN = !USEL;
PIN = !RD;
PIN = !WR;
PIN = !CS1;
PIN = !CS3;
PIN = !VPA;

/* *************** INTERNAL NODES *********************/
NODE SELECT; /* HACK: How do we make this !SELECT ok */
NODE DTACK_ENABLE;
NODE Cb, Ca;

/* *************** EQUATIONS *********************/

FIELD addr = [A19..5];

/* The binary quantity in the compound AND expression represents
   the S's in the below selection string.
   So
   I is from IOSEL, and comes from [A23..20]
   S...S is specified below
   C selects CS3 vs CS1 in the IDE
   RRR selects the IDE register
   x is the LSB and may not be relevant e.g. for 16-bit access

   IIII_SSSS_SSSS_SSSS_SSSC_RRRx
   1111_1000_0000_0010_010C_RRRx
 0x   F    8    0    0  4/5  F-0
*/

/* Does the ! work ? */
SELECT = !(IOSEL & addr:'b'XXXX100000000000010XXXXX);

DTACK.sp = 'b'0;
DTACK_ENABLE.sp = 'b'0;
DTACK.oe = DTACK_ENABLE;
Cb.sp = 'b'0;
Ca.sp = 'b'0;

DS = UDS # LDS;
FIELD STATE = [Cb, Ca, DTACK, DTACK_ENABLE];

SEQUENCE STATE {
    PRESENT 'b'0000
        IF SELECT & DS & R_nW
            NEXT 'b'1100;
        IF SELECT & DS & !R_nW
            NEXT 'b'0001;
        DEFAULT NEXT 'b'0000;
    /* READ Sequence */
    PRESENT 'b'1100
        NEXT 'b'1101;
    PRESENT 'b'1101
        IF !DS
            NEXT 'b'1111;
        DEFAULT NEXT 'b'1101;
    PRESENT 'b'1111
        NEXT 'b'1110;
    PRESENT 'b'1110
        NEXT 'b'0000;
    /* WRITE Sequence */
    PRESENT 'b'0001
        IF !DS
            NEXT 'b'0011;
        DEFAULT NEXT 'b'0001;
    PRESENT 'b'0011
        NEXT 'b'0010;
    PRESENT 'b'0010
        NEXT 'b'0000;
    /* These should not happen normally */
    PRESENT 'b'0100
        NEXT 'b'0000;
    PRESENT 'b'0101
        NEXT 'b'0000;
    PRESENT 'b'0110
        NEXT 'b'0000;
    PRESENT 'b'0111
        NEXT 'b'0000;
    PRESENT 'b'1000
        NEXT 'b'0000;
    PRESENT 'b'1001
        NEXT 'b'0000;
    PRESENT 'b'1010
        NEXT 'b'0000;
    PRESENT 'b'1011
        NEXT 'b'0000;
}

RD =  R_nW & SELECT & DS & Ca;
WR = !R_nW & SELECT & DS;

CS1 = SELECT & !A4;
CS3 = SELECT & A4;

USEL = SELECT & UDS;
LSEL = SELECT & LDS;

VPA = 'b'1;
VPA.oe = (FC2 & FC1 & FC0) & (!A3 & A2 & A1);
